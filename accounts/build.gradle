buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.7.2'
        classpath libs.flyway.postgres
    }
}

plugins {
    id 'java'
    alias(libs.plugins.jooq)
    alias(libs.plugins.flyway)
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation libs.provve.api.dto
    implementation 'provve-backend:notification'
    implementation 'provve-backend:payment'

    implementation libs.vertx.auth.jwt
    implementation libs.jooq.postgres
    implementation libs.bundles.jjwt

    implementation libs.mapstruct
    annotationProcessor libs.mapstruct.processor

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    implementation libs.avaje
    compileOnly libs.avaje.config
    annotationProcessor libs.avaje.generator


    testImplementation libs.bundles.testing
    testImplementation libs.avaje.test
    testAnnotationProcessor libs.avaje.generator
    testImplementation libs.testcontainers.postgresql
    testImplementation libs.testcontainers.junit

    testCompileOnly libs.simple.mail
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/provve'
    user = 'postgres'
    password = '1'
    schemas = ['accounts_history', 'accounts']
}

jooq {
    configuration {
        jdbc {
            driver = "org.postgresql.Driver"
            url = 'jdbc:postgresql://localhost:5432/provve'
            user = 'postgres'
            password = '1'
        }
        generator {
            database {
                inputSchema = "accounts"
            }
            generate {
                fluentSetters = true
                javaTimeTypes = true
                globalObjectReferences = false // выключить артефакты кроме...
                globalTableReferences = true
                globalKeyReferences = true
            }
            target {
                packageName = "tech.provve.accounts.db.generated"
                locale = 'ru'
                directory = "/src/main/java/"
            }
        }
    }
}

tasks.named("jooqCodegen") {
    dependsOn(tasks.named("flywayMigrate"))

    // Use Flyway migration scripts as input to code generation, to avoid
    // task execution when unnecessary
    inputs.files(fileTree("src/main/resources/db/migration"))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

processResources {
    filesMatching("application.yaml") {
        expand([
                auth_secret : System.getenv('AUTH_JWT_SECRET'),
                reset_secret: System.getenv('RESET_JWT_SECRET')
        ])
    }
}