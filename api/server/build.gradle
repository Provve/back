import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        // добавить мой генератор в список доступных
        classpath "tech.provve.openapi-generators:java-vertx-web-with-uuid:1.0"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    alias(libs.plugins.openapi.generator)
    alias(libs.plugins.shadow)
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'provve-backend:accounts'
    implementation 'provve-backend:notification'
    implementation 'provve-backend:payment'

    implementation libs.amazon.s3
    implementation libs.bundles.vertx
    implementation libs.jackson.databind
    implementation libs.slf4j
    compileOnly libs.simple.mail
    compileOnly libs.jooq

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    implementation libs.avaje
    implementation libs.avaje.config
    annotationProcessor libs.avaje.generator
    runtimeOnly libs.mockito.core // для avaje
}

tasks.openApiGenerate {
    dependsOn(build)

    generatorName = 'java-vertx-web-uuid'
    inputSpec = "${rootProject.layout.projectDirectory}/provve-api.yaml"
    outputDir = "${layout.projectDirectory}"
    configOptions = [
            invokerPackage: "tech.provve.api.server.generated",
            apiPackage    : "tech.provve.api.server.generated.api",
            modelPackage           : "tech.provve.api.server.generated.dto",
            hideGenerationTimestamp: "true"
    ]
    templateDir = "${layout.projectDirectory}/src/main/resources/templates"
}

processResources {
    from("$rootProject.layout.projectDirectory") {
        include 'provve-api.yaml'
    }
    from(rootProject.layout.buildDirectory.file('/specs')) {
        include 'antifraud-api.yaml'
        into('build/specs')
    }
}

def mainVerticle = 'tech.provve.api.server.ApiServer'
run {
    mainClass = 'io.vertx.core.Launcher'
    args = ['run', mainVerticle]
}

tasks.register('buildDto', org.gradle.jvm.tasks.Jar) {
    from(sourceSets.main.output.classesDirs) {
        include '**/dto/**'
    }
}

publishing {
    publications {
        onlyDto(MavenPublication) {
            group = 'tech.provve.api'
            artifactId = 'server-dto'
            version = '0.0.1'
            artifact(tasks.named('buildDto'))
        }
    }
    repositories {
        mavenLocal()
    }
}

jar.enabled = false // чтобы не генерить второй jar

def version = libs.versions.provve.backend.get()
tasks.named('shadowJar', ShadowJar) {
    archiveVersion.set(version)
    archiveClassifier.set(null)
    excludes = [
            'templates/*.mustache',
            'db/migration/**',
            '/*.txt'
    ]
    mergeServiceFiles()

    // так как модули собираются в монолит, их конфиги также нужно объдинять
    transform(AppendingTransformer) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        resource = 'application.yaml'
    }

//    minimize()

    manifest {
        attributes(
                'Main-Class': 'io.vertx.core.Launcher',
                'Main-Verticle': mainVerticle
        )
    }
}